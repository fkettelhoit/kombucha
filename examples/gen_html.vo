// A simple html generator

:escape = (:str => { str })

:gen-inline = (:inline => {
    match(inline) with: [
        Em(:content) -> { ["<em>", escape(content), "</em>"] }
        Strong(:content) -> { ["<strong>", escape(content), "</strong>"] }
        Code(:content) -> { ["<code>", escape(content), "</code>"] }
        Link([["href", :href], ["text", :text]]) -> { [#"<a href=""#, href, #"">"#, escape(text), "</a>"] }
        :string -> { [escape(string)] }
    ]
})

:gen-block = (:gen-block ~> { :block => {
    :p = (:inline -> {
        [flatten [["<p>"], inline |> map(gen-inline) |> flatten, ["</p>"]]]
    })
    match (block) with: [
        Subsection([["title", :title], ["blocks", :blocks]]) -> { flatten [
            [
                ["<h3>", escape(title), "</h3>"]
            ]
            blocks |> map(gen-block) |> flatten
        ] }
        List(:blocks) -> {
            :gen-item = (:item => { flatten [
                [
                    ["<li>"]
                ]
                gen-block(item)
                [
                    ["</li>"]
                ]
            ]})
            flatten [
                [
                    ["<ul>"]
                ]
                blocks |> map(gen-item) |> flatten
                [
                    ["</ul>"]
                ]
            ]
        }
        Code(:code) -> { [
            ["<pre><code>", escape(code), "</code></pre>"]
        ] }
        P(:inline) -> { p(inline) }
        :inline -> { [inline |> map(gen-inline) |> flatten] }
    ]
} })

:gen-section = ([:title, :section] => {
    match (section) with: [
        Note([["date", [:yy, :mm, :dd]]]) -> {
            [
                [#"<p><a href="..#notes">Notes</a><br />"#, yy, "/", mm, "/", dd, "</p>"]
                ["<h1>", escape(title), "</h1>"]
            ]
        }
        Section([["title", :title], ["blocks", :blocks]]) -> { flatten [
            [
                ["<h2>", escape(title), "</h2>"]
            ]
            blocks |> map(gen-block) |> flatten
        ] }
    ]
})

:gen-html = (:page => {
    match (page) with: [
        Html([["lang", :lang], ["stylesheet", :stylesheet], ["title", :title], ["sections", :sections]]) -> { flatten [
            [
                ["<!DOCTYPE html>"]
                [#"<html lang=""#, escape(lang), #"">"#]
                ["<head>"]
                [#"<meta charset="utf-8" />"#]
                [#"<meta name="viewport" content="width=device-width, initial-scale=1" />"#]
                [#"<link rel="stylesheet" href=""#, escape(stylesheet), #"" />"#]
                ["<title>", escape(title), "</title>"]
                ["</head>"]
                ["<body>"]
            ]
            sections |> map(gen-section(title)) |> flatten
            [
                ["</body>"]
                ["</html>"]
            ]
        ] }
    ]
})

gen-html(read!())
