// A simple html generator

::escape(:str) = { str }

::gen-inline(:inline) = {
    match(inline) with: [
        Em(:content) -> { ["<em>", escape(content), "</em>"] }
        Strong(:content) -> { ["<strong>", escape(content), "</strong>"] }
        Code(:content) -> { ["<code>", escape(content), "</code>"] }
        Link([["href", :href], ["text", :text]]) -> { [#"<a href=""#, href, #"">"#, escape(text), "</a>"] }
        :string -> { [escape(string)] }
    ]
}

::gen-block(:pre, :post, :block) = {
    match (block) with: [
        Subsection([["title", :title], ["blocks", :blocks]]) -> { [
            "<h3>", escape(title), "</h3>"
            blocks |> map(gen-block(pre, post))
        ] }
        List(:blocks) -> { [
                "<ul>", blocks |> map(:item => { ["<li>", gen-block("", "", item), "</li>"] }), "</ul>"
        ] }
        Code(:code) -> { ["<pre><code>", escape(code), "</code></pre>"] }
        :inline -> { [pre, inline |> map(gen-inline), post] }
    ]
}

::gen-section(:title, :section) = {
    match (section) with: [
        Note([["date", [:yy, :mm, :dd]]]) -> { [
            #"<p><a href="..#notes">Notes</a><br />"#, yy, "/", mm, "/", dd, "</p>"
            "<h1>", escape(title), "</h1>"
        ] }
        Section([["title", :title], ["blocks", :blocks]]) -> { [
            "<h2>", escape(title), "</h2>"
            blocks |> map(gen-block("<p>", "</p>"))
        ] }
        Section([["title", :title], ["id", :id], ["blocks", :blocks]]) -> { [
            #"<h2 id=""#, id, #"">"#, escape(title), "</h2>"
            blocks |> map(gen-block("<p>", "</p>"))
        ] }
    ]
}

::gen-html(:page) = {
    match (page) with: [
        Html([["lang", :lang], ["stylesheet", :stylesheet], ["title", :title], ["sections", :sections]]) -> { [
            "<!DOCTYPE html>"
            #"<html lang=""#, escape(lang), #"">"#
            "<head>"
            #"<meta charset="utf-8" />"#
            #"<meta name="viewport" content="width=device-width, initial-scale=1" />"#
            #"<link rel="stylesheet" href=""#, escape(stylesheet), #"" />"#
            "<title>", escape(title), "</title>"
            "</head>"
            "<body>"
            sections |> map(gen-section(title))
            "</body>"
            "</html>"
        ] }
    ]
}

read!() |> gen-html |> deep-flatten
