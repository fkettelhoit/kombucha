// A simple html generator

:escape = (:str => { str })

:gen-inline = (:inline => {
    match(inline) with: [
        Em(:content) -> { ["<em>", escape(content), "</em>"] }
        Strong(:content) -> { ["<strong>", escape(content), "</strong>"] }
        Code(:content) -> { ["<code>", escape(content), "</code>"] }
        Link([["href", :href], ["text", :text]]) -> { [#"<a href=""#, href, #"">"#, escape(text), "</a>"] }
        :string -> { [escape(string)] }
    ]
})

:gen-block = (:gen-block ~> { :block => {
    match (block) with: [
        Paragraph(:inline) -> { [
            flatten [
                ["<p>"], inline |> map(gen-inline) |> flatten, ["</p>"]
            ]
        ] }
        Subsection([["title", :title], ["blocks", :blocks]]) -> { flatten [
            [
                ["<h3>", escape(title), "</h3>"]
            ]
            blocks |> map(gen-block) |> flatten
        ] }
        Unordered(:blocks) -> {
            :gen-item = (:item => { flatten [
                [
                    ["<li>"]
                ]
                gen-block(item)
                [
                    ["</li>"]
                ]
            ]})
            flatten [
                [
                    ["<ul>"]
                ]
                blocks |> map(gen-item) |> flatten
                [
                    ["</ul>"]
                ]
            ]
        }
        Code(:code) -> { [
            ["<pre><code>", escape(code), "</code></pre>"]
        ] }
    ]
} })

:gen-section = (:section => {
    match (section) with: [
        Section([["title", :title], ["blocks", :blocks]]) -> { flatten [
            [
                ["<h2>", escape(title), "</h2>"]
            ]
            blocks |> map(gen-block) |> flatten
        ] }
    ]
})

:gen-html = (:page => {
    match (page) with: [
        Html([["lang", :lang], ["stylesheet", :stylesheet], ["title", :title], ["sections", :sections]]) -> { flatten [
            [
                ["<!DOCTYPE html>"]
                [#"<html lang=""#, escape(lang), #"">"#]
                ["<head>"]
                [#"<meta charset="utf-8" />"#]
                [#"<meta name="viewport" content="width=device-width, initial-scale=1" />"#]
                [#"<link rel="stylesheet" href=""#, escape(stylesheet), #"" />"#]
                ["<title>", escape(title), "</title>"]
                ["</head>"]
                ["<body>"]
                ["<h1>", escape(title), "</h1>"]
            ]
            sections |> map(gen-section) |> flatten
            [
                ["</body>"]
                ["</html>"]
            ]
        ] }
    ]
})

gen-html(read!())
